FlowNote {
  Expression
    = NodeDefinition
    | FlowDefinition
    | Path

  FlowDefinition = "flow" label "(" HttpMethods "/" ListOf<label, "/"> ")" "=" NonemptyListOf<Nodes, Channel>

  NodeDefinition = "node" label Properties? "=" Actions

  Path = StandardNode Channel NonemptyListOf<Nodes, Channel>

  Nodes
    = Milestone
    | Node

  Milestone = Node "*"

  Node
    = SilentNode
    | IdentityNode
    | StandardNode
  SilentNode = StandardNode "$"
  IdentityNode = StandardNode "#" label
  StandardNode = label
  
  Channel
    = ErrorChannel
    | PlainChannel
    | NamedChannel
    | WaitsFor

  WaitsFor = "..."
  ErrorChannel = "-" label Properties? "!"
  PlainChannel = "-" Properties? ">"
  NamedChannel = "-" label Properties? ">"

  Actions = ListOf<label, ",">

  Properties = "{" ListOf<Property, ","> "}"
  Property = label ":" label

  HttpMethods
    = "GET"
    | "POST"
    | "PUT"
    | "DELETE"
    | "OPTIONS"
    | "HEAD"

  label = (alnum|"_")+
  string  (a string literal) = "\"" (~"\"" ~"\n" any)* "\""
  number  (a number literal)
    = digit* "." digit+  -- fract
    | digit+             -- whole
  space += comment
  comment
    = "/*" (~"*/" any)* "*/"  -- multiLine
    | "//" (~"\n" any)*       -- singleLine
}
