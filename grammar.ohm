FlowNote {
  Expression
    = NodeDefinition
    | FlowDefinition
    | Path
    | Import
    | LinguisticNodeDefinition
    | LinguisticFlowDefinition
    | LinguisticPath
    | EmptyListOf<Nodes, Channel>

  FlowDefinition = "flow" label "(" HttpMethods "/" ListOf<label, "/"> ")" Properties? "=" NonemptyListOf<Nodes, Channel>
  LinguisticFlowDefinition = Concept "Flow" "(" HttpMethods "/" ListOf<label, "/"> ")" Properties? "is" NonemptyListOf<LinguisticNodes, LinguisticPathSeparator>

  NodeDefinition = "node" label Properties? "=" Actions
  LinguisticNodeDefinition = Concept "Node" "is" LinguisticActions

  Actions = ListOf<label, ",">
  LinguisticActionsPlural = ListOf<Concept, ","> "and" Concept
  LinguisticActionsSingular = Concept
  LinguisticActions
    = LinguisticActionsPlural
    | LinguisticActionsSingular

  Path = StandardNode Channel NonemptyListOf<Nodes, Channel>
  LinguisticPath = LinguisticStandardNode LinguisticChannel NonemptyListOf<LinguisticNodes, LinguisticPathSeparator>

  LinguisticPathSeparator = "that" LinguisticChannel

  Import = "import" "\"" label "."? label? "\""

  Nodes
    = Milestone
    | Node

  LinguisticNodes
    = LinguisticMilestone
    | LinguisticNode

  Milestone = Node "*"
  LinguisticMilestone = LinguisticNode "then commits"

  Node
    = WaitsFor
    | NodeBase

  WaitsFor = NodeBase "..." StandardNode

  NodeBase
    = SilentNode
    | IdentityNode
    | StandardNode
  SilentNode = StandardNode "$"
  IdentityNode = StandardNode "#" label
  StandardNode = label
  
  LinguisticNode
    = LinguisticWaitsFor
    | LinguisticNodeBase

  LinguisticWaitsFor = LinguisticNodeBase "but waits for" LinguisticStandardNode

  LinguisticNodeBase
    = LinguisticSilentNode
    | LinguisticIdentityNode
    | LinguisticStandardNode
  LinguisticSilentNode = "a silent" LinguisticStandardNode
  LinguisticIdentityNode = LinguisticStandardNode "(as" Concept ")"
  LinguisticStandardNode = Concept

  Concept = NonemptyListOf<label, space>

  Channel
    = ErrorChannel
    | PlainChannel
    | NamedChannel

  ErrorChannel = "-" label Properties? "!"
  PlainChannel = "-" Properties? ">"
  NamedChannel = "-" label Properties? ">"

  LinguisticChannel
    = LinguisticErrorChannel
    | LinguisticPlainChannel
    | LinguisticNamedChannel

  LinguisticErrorChannel = "errors with" Concept Properties? "to"
  LinguisticPlainChannel = "connects" Properties? "to"
  LinguisticNamedChannel = "connects with" Concept Properties? "to"


  Properties = "{" ListOf<Property, ","> "}"
  Property = label ":" label

  HttpMethods
    = "GET"
    | "POST"
    | "PUT"
    | "DELETE"
    | "OPTIONS"
    | "HEAD"

  label = (alnum|"_")+
  string  (a string literal) = "\"" (~"\"" ~"\n" any)* "\""
  number  (a number literal)
    = digit* "." digit+  -- fract
    | digit+             -- whole
  space += comment
  comment
    = "/*" (~"*/" any)* "*/"  -- multiLine
    | "//" (~"\n" any)*       -- singleLine
}
